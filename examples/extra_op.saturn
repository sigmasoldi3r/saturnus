use { entries, `+?` } in std;

fn `|>`(left, right) {
  return right(left);
}

fn `<|`(left, right) {
  return left(right);
}

// Example usage of extra operators:
let r = 101
  |> (a => a + 10)
  |> (a => a * 2)
  |> tostring;

let kind = type <| r |> a => "(" ++ a ++ ")";

print("Result = " ++ r ++ " " ++ kind);
print("Maybe sum", () +? 10);
print("Maybe sum", 10 +? 10);

fn `<-`(a, b) {
  return tonumber(tostring(a) ++ tostring(b));
}

// With the use of some operators, you must be aware that
// Saturnus is space-sensitive in some cases:
let a = 10 < -5;
let b = 10 <- 5;
print("Difference between operators:", a, b);

// Custom operators forbid implicit assignment,
// can be solved using builder patterns.
fn `->>`(a, b) {
  return a ++ ", " ++ b;
}
fn `->>=`(b, x) {
  b._0 = b._0 ->> x;
  return b;
}

let x = ("a list", 0);
x ->>= "of";
x ->>= "items";
x ->>= "maybe";
print(x._0);
